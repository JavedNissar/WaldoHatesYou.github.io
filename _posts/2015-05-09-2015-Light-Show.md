---
title: Light Show
layout: post
---
**Light Show** involves the use of periodic functions to generate interesting color sequences in an RGB LED. The periodic functions utilised by my particular system are \\(f(x)=(255/2)sin(x)+(255/2)\\), \\(g(x)=(255/2)cos(x)+(255/2)\\) and \\(h(x)=(255/2)sin(cos(x))+(255/2)\\). The transformations performed on these functions were done because the RGB LED takes values in the range 0 to 255 and as a result, the base functions \\(f(x)=sin(x)\\),\\(g(x)=cos(x)\\), and \\(h(x)=sin(cos(x))\\) had to be transformed so that they would output values in the correct range. That being said, the circuit itself was wired such that the RGB LED would receive inputs from the PWM digital outputs of an Arduino Uno microcontroller with PWM being a technique that involves the electrical signal from the Arduino board being turned on and off at an interval determined by the value from 0 to 255. This value translates to a fraction of the duty cycle with the duty cycle being the amount of time, the LED spends on or off. For example, 255 means a 100% duty cycle and 127 means a 50% duty cycle. Meanwhile, the code that runs this device is quite simple.
{%highlight C %}
const int greenPin=9;
const int bluePin=10;
const int redPin=11;
int number=0;
void setup() {
  Serial.begin(9600);
  
  pinMode(greenPin,OUTPUT);
  pinMode(bluePin,OUTPUT);
  pinMode(redPin,OUTPUT);
}
{% endhighlight %}
The setup code above just sets the pin numbers controlling the red, green, and blue LEDs that are inside the RGB LED. The setup code above just sets the pins to be digital outputs and begins a serial connection for debugging purposes.
{%highlight C %}
double modifiedSine(float angle){
  return 127.5*sin(angle)+127.5;
}
double modifiedCosine(float angle){
  return 127.5*cos(angle)+127.5;
}
double modifiedCombo(float angle){
  return 127.5*sin(5*cos(angle))+127.5;
}
double input(int termNumber){
  return PI/8*termNumber;
}
{% endhighlight %}
These are a set of helper functions which provide easy access to the functions mentioned at the beginning of this post as well as a function that creates a nice input for those periodic functions. The input itself is the sequence \\(\frac{\pi{n}}{8}\\) where n is an integer.
{%highlight C%}
void loop() {
  // put your main code here, to run repeatedly:
  double currentInput=input(number);
  int redValue=(int)round(modifiedSine(currentInput));
  int blueValue=(int)round(modifiedCosine(currentInput));
  int greenValue=(int)round(modifiedCombo(currentInput));
  
  Serial.print("Mapped values \t Red: ");
  Serial.print(redValue);
  Serial.print("\t Green: ");
  Serial.print(greenValue);
  Serial.print("\t Blue: ");
  Serial.println(blueValue);
  
  analogWrite(redPin,redValue);
  analogWrite(bluePin,blueValue);
  analogWrite(greenPin,greenValue);
  number++;
  delay(250);
}
{%endhighlight%}
The code above runs repeatedly on the Arduino and it gets the current input using the sequence mentioned earlier. Using that, three integer values are produced using the periodic functions from earlier. The periodic functions must be rounded and converted to an integer because they output doubles and a double is not taken by the analogWrite function which is used for outputting a PWM signal. Finally, the number variable is incremented at every loop and a delay of 250 ms is applied to ensure that the colors do not fade too quickly.